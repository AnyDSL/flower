type Mask = Mask3;
type MaskSep = Mask3Sep;

// data layout padded for Dirichlet
// image: (8 + width + 8) x (1 + height + 1)
static pad_x = 2*8;
static pad_y = 2;
fn get_pixel(acc : Acc, x : int, y : int) -> float {
    acc.img.data((acc.roi.o_y + acc.roy + y + pad_y/2)*acc.img.stride + acc.roi.o_x + acc.rox + x + pad_x/2)
}
fn set_pixel(mut acc : Acc, x : int, y : int, val : float) -> () {
    acc.img.data((acc.roi.o_y + acc.roy + y + pad_y/2)*acc.img.stride + acc.roi.o_x + acc.rox + x + pad_x/2) = val;
}

fn apply_stencil(mask : Mask, acc : Acc, x : int, y : int) -> float {
    let mut sum = 0.0f;
    let half_range_y = mask.size_y / 2;
    let half_range_x = mask.size_x / 2;

    for j in iterate(-half_range_y, half_range_y+1) {
        for i in iterate(-half_range_x, half_range_x+1) {
            let stencil_val = mask.data(j + half_range_y)(i + half_range_x);
            if stencil_val != 0.0f {
                let mut idx_x = acc.roi.o_x + acc.rox + x + i;
                let mut idx_y = acc.roi.o_y + acc.roy + y + j;

                // 0 --- 1 --- 2
                // 3 --- 4 --- 5
                // 6 --- 7 --- 8

                // left
                if acc.region == 0 { idx_x = mirror_lower(idx_x, acc.roi.o_x); }
                if acc.region == 3 { idx_x = mirror_lower(idx_x, acc.roi.o_x); }
                if acc.region == 6 { idx_x = mirror_lower(idx_x, acc.roi.o_x); }

                // right
                if acc.region == 2 { idx_x = mirror_upper(idx_x, acc.roi.o_x + acc.roi.width); }
                if acc.region == 5 { idx_x = mirror_upper(idx_x, acc.roi.o_x + acc.roi.width); }
                if acc.region == 8 { idx_x = mirror_upper(idx_x, acc.roi.o_x + acc.roi.width); }

                // top
                if acc.region == 0 { idx_y = mirror_lower(idx_y, acc.roi.o_y); }
                if acc.region == 1 { idx_y = mirror_lower(idx_y, acc.roi.o_y); }
                if acc.region == 2 { idx_y = mirror_lower(idx_y, acc.roi.o_y); }

                // bottom
                if acc.region == 6 { idx_y = mirror_upper(idx_y, acc.roi.o_y + acc.roi.height); }
                if acc.region == 7 { idx_y = mirror_upper(idx_y, acc.roi.o_y + acc.roi.height); }
                if acc.region == 8 { idx_y = mirror_upper(idx_y, acc.roi.o_y + acc.roi.height); }

                // all in
                if acc.region == 10 {
                    idx_x = mirror_lower(idx_x, acc.roi.o_x);
                    idx_x = mirror_upper(idx_x, acc.roi.o_x + acc.roi.width);
                    idx_y = mirror_upper(idx_y, acc.roi.o_y + acc.roi.height);
                    idx_y = mirror_lower(idx_y, acc.roi.o_y);
                }

                sum += acc.img.data((idx_y + pad_y/2) * acc.img.stride + idx_x + pad_x/2) * stencil_val;
            } else {
                // additional logic for empty stencil value
            }
        }
    }

    sum
}


// Deriv 2D
fn deriv2D(img : Img, RHS : Img) -> () {
    let mask = Mask {
        data : [[0.0f,  1.0f, 0.0f],
                [1.0f, -4.0f, 1.0f],
                [0.0f,  1.0f, 0.0f]],
        size_x : 3, size_y : 3
    };
    for x, y, mask, img_acc, rhs_acc in @iteration(mask, img, RHS) {
        let val = apply_stencil(mask, img_acc, x, y);
        set_pixel(rhs_acc, x, y, val);
    }
}


// Jacobi
static omega = 0.8f;
static jacobi_mask = Mask {
    data : [[0.00f, 0.25f, 0.00f],
            [0.25f, 0.00f, 0.25f],
            [0.00f, 0.25f, 0.00f]],
    size_x : 3, size_y : 3
};
fn jacobi_solver(x : int, y : int, mask : Mask, RHS : Acc, Tmp : Acc, Sol : Acc) -> () {
    let val = (1.0f - omega) * get_pixel(Tmp, x, y) +
              0.25f * omega  * get_pixel(RHS, x, y) +
              apply_stencil(mask, Tmp, x, y);
    set_pixel(Sol, x, y, val);
}

fn jacobi(RHS : Img, Tmp : Img, Sol : Img, omega : float) -> () {
    for x, y, mask, mut tmp_acc, sol_acc in @iteration(jacobi_mask, Tmp, Sol) {
        // TODO: RHS is not mapped in iteration
        let rhs_acc = get_acc(RHS, 10);
        tmp_acc.region = 4; // no bh
        let val = (1.0f - omega) * get_pixel(tmp_acc, x, y) +
                  0.25f * omega  * get_pixel(rhs_acc, x, y) +
                  apply_stencil(mask, tmp_acc, x, y);
        set_pixel(sol_acc, x, y, val);
    }
}


// Residual
static residual_mask = Mask {
    data : [[0.0f,  1.0f, 0.0f],
            [1.0f, -4.0f, 1.0f],
            [0.0f,  1.0f, 0.0f]],
    size_x : 3, size_y : 3
};
fn residual_fun(x : int, y : int, mask : Mask, Sol : Acc, Res : Acc, RHS : Acc) -> () {
    let val = get_pixel(RHS, x, y) + apply_stencil(mask, Sol, x, y);
    set_pixel(Res, x, y, val);
}

fn residual(Sol : Img, Res : Img, RHS : Img) -> () {
    for x, y, mask, mut sol_acc, res_acc in @iteration(residual_mask, Sol, Res) {
        // TODO: RHS is not mapped in iteration
        let rhs_acc = get_acc(RHS, 10);
        sol_acc.region = 4; // no bh
        let val = get_pixel(rhs_acc, x, y) +
                  apply_stencil(mask, sol_acc, x, y);
        set_pixel(res_acc, x, y, val);
    }
}


// Restrict: arr = size x size; out = size/2 x size/2; fine -> coarse
// coarse = Stencil * fine;
static restrict_mask = Mask {
    data : [[0.25f, 0.50f, 0.25f],
            [0.50f, 1.00f, 0.50f],
            [0.25f, 0.50f, 0.25f]],
    size_x : 3, size_y : 3
};
fn restrict_fun(x : int, y : int, mask : Mask, Res : Acc, RHS : Acc) -> () {
    let val = get_pixel(Res, 2*x,   2*y  ) +
              get_pixel(Res, 2*x+1, 2*y  ) +
              get_pixel(Res, 2*x,   2*y+1) +
              get_pixel(Res, 2*x+1, 2*y+1);
    set_pixel(RHS, x, y, val);
}

fn restrict(Res : Img, RHS : Img) -> () {
    for x, y, mask, res_acc, rhs_acc in @iteration(restrict_mask, Res, RHS) {
        let val = get_pixel(res_acc, 2*x,   2*y  ) +
                  get_pixel(res_acc, 2*x+1, 2*y  ) +
                  get_pixel(res_acc, 2*x,   2*y+1) +
                  get_pixel(res_acc, 2*x+1, 2*y+1);
        set_pixel(rhs_acc, x, y, val);
    }
}


// Interpolate: arr = size/2 x size/2; out = size x size; coarse -> fine
// fine += Stencil * coarse:
static interpolate_mask = Mask {
    data : [[0.25f, 0.50f, 0.25f],
            [0.50f, 1.00f, 0.50f],
            [0.25f, 0.50f, 0.25f]],
    size_x : 3, size_y : 3
};
fn interpolate_fun(x : int, y : int, mask : Mask, SolC : Acc, SolF : Acc) -> () {
    let value = get_pixel(SolC, x, y);
    set_pixel(SolF, 2*x  , 2*y  , get_pixel(SolF, 2*x  , 2*y  ) + value);
    set_pixel(SolF, 2*x+1, 2*y  , get_pixel(SolF, 2*x+1, 2*y  ) + value);
    set_pixel(SolF, 2*x  , 2*y+1, get_pixel(SolF, 2*x  , 2*y+1) + value);
    set_pixel(SolF, 2*x+1, 2*y+1, get_pixel(SolF, 2*x+1, 2*y+1) + value);
}

fn interpolate(SolC : Img, SolF : Img) -> () {
    for x, y, mask, solf_acc, solc_acc in @iteration(interpolate_mask, SolF, SolC) {
        let value = get_pixel(solc_acc, x, y);
        set_pixel(solf_acc, 2*x  , 2*y  , get_pixel(solf_acc, 2*x  , 2*y  ) + value);
        set_pixel(solf_acc, 2*x+1, 2*y  , get_pixel(solf_acc, 2*x+1, 2*y  ) + value);
        set_pixel(solf_acc, 2*x  , 2*y+1, get_pixel(solf_acc, 2*x  , 2*y+1) + value);
        set_pixel(solf_acc, 2*x+1, 2*y+1, get_pixel(solf_acc, 2*x+1, 2*y+1) + value);
    }
}


// L2 norm
fn l2norm(img : Img) -> float {
    // TODO: runs only on the CPU atm
    let mut l2sum = 0.0f;
    for y in $iterate(0, img.height) {
        for x in $iterate(0, img.width) {
            let value = img.data((y+pad_y/2) * img.stride + x+pad_x/2);
            l2sum += value*value;
        }
    }
    sqrtf(l2sum)/(img.width*img.height as float)
}


// normal V-Cycle, components are hard-coded
fn vcycle(RHS0 : Img, levels : int, vsteps : int, ssteps : int) -> Img {
    let width   = RHS0.width;
    let height  = RHS0.height;
    let mut RHS = ~[levels:Img];
    let mut Sol = ~[levels:Img];
    let mut Tmp = ~[levels:Img];
    let mut Res = ~[levels:Img];
    for level in range(0, levels) {
        fn init_img(level : int) -> Img {
            Img {
                data : ~[((width >> level) + pad_x) * ((height >> level) + pad_y): float],
                width : width >> level,
                height : height >> level,
                stride : (width >> level) + pad_x
            }
        }
        if level == 0 {
            RHS(level) = RHS0;
        } else {
            RHS(level) = init_img(level);
        }
        Sol(level) = init_img(level);
        Res(level) = init_img(level);
        Tmp(level) = init_img(level);
        $init_zero(Tmp(level).data, Tmp(level).stride, Tmp(level).height + pad_y);
    }

    fn vcycle_intern(level : int) -> () {
        if level == levels-1 {
            for i in range(0, ssteps) {
                // smooth
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                jacobi(RHS(level), Tmp(level), Sol(level), omega);
            }
        } else {
            // pre-smooth
            for i in range(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                jacobi(RHS(level), Tmp(level), Sol(level), omega);
            }

            residual(Sol(level), Res(level), RHS(level));
            restrict(Res(level), RHS(level+1));

            //if level == 0 { thorin_print_gflops(l2norm(Res(level))); }

            $init_zero(Tmp(level+1).data, Tmp(level+1).stride, Tmp(level+1).height + pad_y);
            vcycle_intern(level+1);

            interpolate(Sol(level+1), Sol(level));

            // post-smooth
            for i in range(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                jacobi(RHS(level), Tmp(level), Sol(level), omega);
            }
        }
    }
    for i in range(0, vsteps) {
        vcycle_intern(0);
    }

    Sol(0)
}


// V-Cycle DSL: solver, etc. can be provided by the user
fn vcycle_dsl(RHS0 : Img, levels : int, vsteps : int, ssteps : int,
          solver : fn(int, int, Mask, Acc, Acc, Acc) -> (), solver_mask : Mask,
          residual : fn(int, int, Mask, Acc, Acc, Acc) -> (), residual_mask : Mask,
          restrict : fn(int, int, Mask, Acc, Acc) -> (), restrict_mask : Mask,
          interpolate : fn(int, int, Mask, Acc, Acc) -> (), interpolate_mask : Mask
         ) -> Img {
    let width   = RHS0.width;
    let height  = RHS0.height;
    let mut RHS = ~[levels:Img];
    let mut Sol = ~[levels:Img];
    let mut Tmp = ~[levels:Img];
    let mut Res = ~[levels:Img];
    for level in range(0, levels) {
        fn init_img(level : int) -> Img {
            Img {
                data : ~[((width >> level) + pad_x) * ((height >> level) + pad_y): float],
                width : width >> level,
                height : height >> level,
                stride : (width >> level) + pad_x
            }
        }
        if level == 0 {
            RHS(level) = RHS0;
        } else {
            RHS(level) = init_img(level);
        }
        Sol(level) = init_img(level);
        Res(level) = init_img(level);
        Tmp(level) = init_img(level);
        $init_zero(Tmp(level).data, Tmp(level).stride, Tmp(level).height + pad_y);
    }

    fn vcycle_dsl_intern(level : int) -> () {
        let rhs_acc = get_acc(RHS(level), 4);
        if level == levels-1 {
            for i in range(0, ssteps) {
                // smooth
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                for x, y, mask, mut tmp_acc, sol_acc in iteration(solver_mask, Tmp(level), Sol(level)) {
                    tmp_acc.region = 4; // no bh
                    solver(x, y, mask, rhs_acc, tmp_acc, sol_acc);
                }
            }
        } else {
            // pre-smooth
            for i in range(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                for x, y, mask, mut tmp_acc, sol_acc in iteration(solver_mask, Tmp(level), Sol(level)) {
                    tmp_acc.region = 4; // no bh
                    solver(x, y, mask, rhs_acc, tmp_acc, sol_acc);
                }
            }

            let merge = true;
            if merge == true {
                iteration_merge(Sol(level), Res(level), RHS(level), RHS(level+1), residual, residual_mask, restrict, restrict_mask);
            } else {
                for x, y, mask, mut sol_acc, res_acc in iteration(residual_mask, Sol(level), Res(level)) {
                    sol_acc.region = 4; // no bh
                    residual(x, y, mask, sol_acc, res_acc, rhs_acc);
                }

                for x, y, mask, res_acc, rhs_acc in iteration(residual_mask, Res(level), RHS(level+1)) {
                    restrict(x, y, mask, res_acc, rhs_acc);
                }
            }

            //if level == 0 { thorin_print_gflops(l2norm(Res(level))); }

            $init_zero(Tmp(level+1).data, Tmp(level+1).stride, Tmp(level+1).height + pad_y);
            vcycle_dsl_intern(level+1);

            for x, y, mask, solf_acc, solc_acc in iteration(interpolate_mask, Sol(level), Sol(level+1)) {
                interpolate(x, y, mask, solc_acc, solf_acc);
            }

            // post-smooth
            for i in range(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                for x, y, mask, mut tmp_acc, sol_acc in iteration(solver_mask, Tmp(level), Sol(level)) {
                    tmp_acc.region = 4; // no bh
                    solver(x, y, mask, rhs_acc, tmp_acc, sol_acc);
                }
            }
        }
    }
    for i in range(0, vsteps) {
        vcycle_dsl_intern(0);
    }

    Sol(0)
}


extern
fn thorin_vcycle(img_data : &[float], out_data : &[float], width : int, height : int) -> () {
    let RHS     = Img { data : ~[(width + pad_x) * (height + pad_y): float], width : width, height : height, stride : width + pad_x };
    let mut img = Img { data : ~[(width + pad_x) * (height + pad_y): float], width : width, height : height, stride : width + pad_x };
    let mut out = Img { data : out_data , width : width, height : height, stride : width };

    for y in range(0, img.height) {
        for x in range(0, img.width) {
            img.data((y+pad_y/2)*img.stride + x+pad_x/2) = img_data(y*img.width + x);
        }
    }

    @deriv2D(img, RHS);

    let levels = 3;
    let vsteps = 1;
    let ssteps = 2;

    let result = @vcycle_dsl(RHS, levels, vsteps, ssteps, jacobi_solver, jacobi_mask, residual_fun, residual_mask, restrict_fun, restrict_mask, interpolate_fun, interpolate_mask);
    //let result = @vcycle(RHS, levels, vsteps, ssteps);

    for y in range(0, out.height) {
        for x in range(0, out.width) {
            out.data(y*out.width + x) = result.data((y+pad_y/2)*result.stride + x+pad_x/2);
        }
    }
}

