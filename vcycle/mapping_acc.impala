fn iteration_merge(RHSC: Img, Sol: Img, RHSF: Img,
                   residual: fn(i32, i32, Acc, Acc, Acc, Mask) -> (), residual_mask: Mask, // RHS + sum(Sol) -> Res
                   restrict: fn(i32, i32, Acc, Acc, Mask) -> (), restrict_mask: Mask       // Res -> RHS(+1)
                  ) -> () {
    let sol_gpu  = get_img(acc_alloc(acc_dev(), Sol.stride  * Sol.height  * 4), Sol.width,  Sol.height);
    let rhsf_gpu = get_img(acc_alloc(acc_dev(), RHSF.stride * RHSF.height * 4), RHSF.width, RHSF.height);
    let rhsc_gpu = get_img(acc_alloc(acc_dev(), RHSC.stride * RHSC.height * 4), RHSC.width, RHSC.height);
    copy(RHSF.buf, rhsf_gpu.buf, rhsf_gpu.width * rhsf_gpu.height * 4);
    copy(Sol.buf,  sol_gpu.buf,  sol_gpu.width  * sol_gpu.height  * 4);

    let step  = 2; // restrict by 2x
    let grid  = (Sol.width, Sol.height, 1);
    let block = (64, step, 1);

    for benchmark_acc() {
        acc(acc_dev(), grid, block, || @{
            let tid_x = acc_tidx();
            let tid_y = acc_tidy();
            let gid_y = acc_gidy();

            let spm_stride = block(0);
            let spm_height = block(1);
            let spm_buf = Buffer { device : acc_dev(), data : reserve_shared(spm_stride * spm_height * 4) };
            let spm_img = get_img(spm_buf, spm_stride, spm_height);

            // residual: index space == block
            let tmp_acc  = get_acc(spm_img, 4 /* no bh */);
            let rhsf_acc = Acc { img : rhsf_gpu, roi : RoI { o_x : 0, o_y : 0, width : rhsf_gpu.width, height : rhsf_gpu.height },
                                                 rox : acc_bdimx() * acc_bidx(), roy : acc_bdimy() * acc_bidy(), region : 4 /* no bh */ };
            let sol_acc  = Acc { img : sol_gpu,  roi : RoI { o_x : 0, o_y : 0, width : sol_gpu.width,  height : sol_gpu.height },
                                                 rox : acc_bdimx() * acc_bidx(), roy : acc_bdimy() * acc_bidy(), region : 4 /* no bh */ };
            residual(tid_x, tid_y, tmp_acc, sol_acc, rhsf_acc, residual_mask);

            acc_barrier();

            // restrict: index space == block
            if tid_x < block(0) / step && gid_y % step == 0 @{
                let rhs_acc = Acc { img : rhsc_gpu, roi : RoI { o_x : 0, o_y : 0, width : rhsc_gpu.width, height : rhsc_gpu.height },
                                                    rox : acc_bdimx() * acc_bidx()/step, roy : acc_bdimy() * acc_bidy()/step, region : 4 /* no bh */ };
                let tmp_acc = get_acc(spm_img, 4 /* no bh */);
                restrict(tid_x, tid_y, rhs_acc, tmp_acc, restrict_mask);
            }
        });
    }

    copy(rhsc_gpu.buf, RHSC.buf, rhsc_gpu.width * rhsc_gpu.height * 4);
    release(sol_gpu.buf);
    release(rhsf_gpu.buf);
    release(rhsc_gpu.buf);
}
