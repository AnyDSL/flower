fn main() -> i32 {
    let length = 4;
    let width  = 4096;
    let height = 4096;
    let arr    = Img { buf : alloc_host(width * height * 4), width : width, height : height, stride : width };
    let out    = Img { buf : alloc_host(width * height * 4), width : width, height : height, stride : width };
    init_rand(arr.buf.data as &[f32], arr.stride, out.height);
    init_zero(out.buf.data as &[f32], out.stride, out.height);

    for x, y, mut out_acc, arr_acc in @iteration(out, arr) {
        fn median() -> f32 {
            let mut tmp_arr: [f32 * 4];
            for i in range(-length/2, +length/2 + 1) {
                let x_clamped = clamp_upper(clamp_lower(x + i, 0, arr_acc.img.width, return), 0, arr_acc.img.width, return);
                tmp_arr(i + length / 2) = (arr_acc.img.buf.data as &[f32])(y * arr_acc.img.stride + x_clamped);
            }

            oddeven_merge_sort(length, tmp_arr);

            tmp_arr((length - 1) / 2)
        }
        (out_acc.img.buf.data as &[f32])(y * out_acc.img.stride + x) = median();
    }
    print_total_timing();

    fn reference() -> i32 {
        let mut passed = 0;
        for y in $range(0, out.height) {
            for x in $range(0, out.width) @{
                fn median() -> f32 {
                    let mut tmp_arr: [f32 * 4];
                    for i in range(-length/2, +length/2 + 1) {
                        let x_clamped = clamp_upper(clamp_lower(x + i, 0, arr.width, return), 0, arr.width, return);
                        tmp_arr(i + length / 2) = (arr.buf.data as &[f32])(y * arr.stride + x_clamped);
                    }
                    sort_f32(length, &tmp_arr);
                    tmp_arr((length - 1) / 2)
                }
                let ref = median();
                if ref != (out.buf.data as &[f32])(y * out.stride + x) {
                    passed++;
                }
            }
        }
        passed
    }

    let result = @reference();

    release(arr.buf);
    release(out.buf);

    result
}
