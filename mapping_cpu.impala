fn iteration(arr : Img, out : Img,
             body : fn(int, int, Acc, Acc) -> ()
            ) -> () {
    let acc_arr = Acc { img : arr, o_x : 0, o_y : 0 };
    let acc_out = Acc { img : out, o_x : 0, o_y : 0 };
    getMicroTime();
    for y in $iterate(0, out.height) {
        for x in $iterate(0, out.width) {
            @body(x, y, acc_arr, acc_out);
        }
    }
    getMicroTime();
}

fn iteration_bounds(ksize : int, arr : Img, out : Img,
                    body : fn(int, int, Acc, Acc, int) -> ()
                   ) -> () {
    let bbh = ksize / 2;
    // (minx, maxx, miny, maxy)
    let Bounds2D = [
        (0              , bbh            , 0               , bbh),
        (bbh            , out.width - bbh, 0               , bbh),
        (out.width - bbh, out.width      , 0               , bbh),

        (0              , bbh            , out.height - bbh, out.height),
        (bbh            , out.width - bbh, out.height - bbh, out.height),
        (out.width - bbh, out.width      , out.height - bbh, out.height),

        (0              , bbh            , bbh             , out.height - bbh),
        (out.width - bbh, out.width      , bbh             , out.height - bbh),
        (bbh            , out.width - bbh, bbh             , out.height - bbh)
    ];

    let acc_arr = Acc { img : arr, o_x : 0, o_y : 0 };
    let acc_out = Acc { img : out, o_x : 0, o_y : 0 };

    for region in @iterate(0, 9) {
        let bounds = Bounds2D(region);
        for y in $iterate(bounds(2), bounds(3)) {
            for x in $iterate(bounds(0), bounds(1)) {
                @body(x, y, acc_arr, acc_out, region);
            }
        }
    }
}

fn iteration_sep(arr : Img, out : Img,
                 body : fn(int, int, Acc, Acc, bool) -> ()
                ) -> () {
    // allocate temporary array
    let tmp = Img { data : ~[out.width*out.height:float], width : out.width, height : out.height };
    init_zero(tmp.data, $tmp.width, $tmp.height);
    let acc_arr = Acc { img : arr, o_x : 0, o_y : 0 };
    let acc_out = Acc { img : out, o_x : 0, o_y : 0 };
    let acc_tmp = Acc { img : tmp, o_x : 0, o_y : 0 };

    getMicroTime();
    for y in $iterate(0, out.height) {
        for x in $iterate(0, out.width) {
            let is_row = false;
            @body(x, y, acc_arr, acc_tmp, is_row);
        }
    }

    for y in $iterate(0, out.height) {
        for x in $iterate(0, out.width) {
            let is_row = true;
            @body(x, y, acc_tmp, acc_out, is_row);
        }
    }
    getMicroTime();
}

fn iteration_sep_bounds(ksize : int, arr : Img, out : Img,
                        body : fn(int, int, Acc, Acc, bool, int) -> ()
                       ) -> () {
    // allocate temporary array
    let tmp = Img { data : ~[out.width*out.height:float], width : out.width, height : out.height };
    init_zero(tmp.data, $tmp.width, $tmp.height);
    let acc_arr = Acc { img : arr, o_x : 0, o_y : 0 };
    let acc_out = Acc { img : out, o_x : 0, o_y : 0 };
    let acc_tmp = Acc { img : tmp, o_x : 0, o_y : 0 };

    let bbh = ksize / 2;

    let Bounds2DCol = [
        (0               , bbh),
        (bbh             , out.height - bbh),
        (out.height - bbh, out.height)
    ];

    let Bounds2DRow = [
        (0              , bbh),
        (bbh            , out.width - bbh),
        (out.width - bbh, out.width)
    ];

    getMicroTime();
    for region in @iterate(0, 3) {
        let bounds = Bounds2DCol(region);
        for y in $iterate(bounds(0), bounds(1)) {
            for x in $iterate(0, out.width) {
                let is_row = false;
                @body(x, y, acc_arr, acc_tmp, is_row, region);
            }
        }
    }

    for region in @iterate(0, 3) {
        let bounds = Bounds2DRow(region);
        for y in $iterate(0, out.height) {
            for x in $iterate(bounds(0), bounds(1)) {
                let is_row = true;
                @body(x, y, acc_tmp, acc_arr, is_row, region);
            }
        }
    }
    getMicroTime();
}

fn iteration_sep_advanced(ksize : int, arr : Img, out : Img,
                          body : fn(int, int, Acc, Acc, bool, int) -> ()
                         ) -> () {
    let bbh = ksize / 2;

    let Bounds2DCol = [
        (0               , bbh),
        (bbh             , out.height - bbh),
        (out.height - bbh, out.height)
    ];

    let Bounds2DRow = [
        (0              , bbh),
        (bbh            , out.width - bbh),
        (out.width - bbh, out.width)
    ];

    getMicroTime();
    // allocate temporary array
    let tmp = Img { data : ~[out.width*out.height:float], width : out.width, height : 1 };
    init_zero(tmp.data, $tmp.width, $tmp.height);
    let acc_arr = Acc { img : arr, o_x : 0, o_y : 0 };
    let acc_out = Acc { img : out, o_x : 0, o_y : 0 };
    let acc_tmp = Acc { img : tmp, o_x : 0, o_y : 0 };

    for region_col in @iterate(0, 3) {
        let bounds_col = Bounds2DCol(region_col);
        for y in $iterate(bounds_col(0), bounds_col(1)) {
            for region_row in @iterate(0, 3) {
                let bounds_row = Bounds2DRow(region_row);
                for x in $iterate(bounds_row(0), bounds_row(1)) {
                    let is_row = false;
                    @body(x, y, acc_arr, acc_tmp, is_row, region_col);
                }
            }
            for region_row in @iterate(0, 3) {
                let bounds_row = Bounds2DRow(region_row);
                for x in $iterate(bounds_row(0), bounds_row(1)) {
                    let is_row = true;
                    @body(x, y, acc_tmp, acc_out, is_row, region_row);
                }
            }
        }
    }
    getMicroTime();
}
