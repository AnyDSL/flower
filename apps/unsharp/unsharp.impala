
fn init_data(arr: Img) -> () {
    let buf = bitcast[&mut[pixel_t]](arr.buf.data);

    for y in range(0, arr.height) {
        for x in range(0, arr.width) {
            let val = if ((((x % 16) >= 8) & (( y % 16) < 8)) | (((x % 16) < 8) & ((y % 16) >= 8))) == false {
                255 as pixel_t
            }
            else {
                0 as pixel_t
            };
            buf(y * arr.stride + x) = val;
        }
    }
}

fn main() -> i32 {
    let width  = 1024;
    let height = 1024;

    static mut ch_in_to_blur    : pipe;
    static mut ch_in_to_sharpen : pipe;
    static mut ch_in_to_mask    : pipe;
    static mut ch_blur_to_sharpen_or_mask : pipe;
    static mut ch_blur_to_sharpen : pipe;
    static mut ch_blur_to_mask    : pipe;
    static mut ch_sharpen_to_mask : pipe;

    let input_image = create_host_img(width, height);
    let out = create_host_img(width, height);

    let blur = create_virtual_img(width, height, &mut ch_blur_to_sharpen_or_mask);
    let sharpen = create_virtual_img(width, height, &mut ch_sharpen_to_mask );

    init_data(input_image);
    init_zero(bitcast[&mut[pixel_t]](out.buf.data), out.stride, out.height);

    let mask = get_mask5(blur_mask5);

    let lower = nobh_center;
    let upper = nobh_center;
    //let lower = clamp_lower;
    //let upper = clamp_upper;
    //let lower = mirro_lower;
    //let upper = mirro_upper;
    let enable_offset = 0;

    for benchmark_host(num_host_iter) {
        let (in_to_blur, in_to_sharpen, in_to_mask) = split_image3(math, input_image, &mut ch_in_to_blur , &mut ch_in_to_sharpen, &mut ch_in_to_mask);

        for math, x, y, out, input_image, mask in iteration(math, blur, in_to_blur, mask, lower,upper) {
            // Blur = Blurx . Blury
            let pix_blur = apply_stencil(x, y, input_image, mask);
            out.write(x, y, pix_blur);

        }

        let (blur_to_sharpen, blur_to_mask) = split_image(math, blur, &mut ch_blur_to_sharpen, &mut ch_blur_to_mask);

        for math, x, y, out, input_image, blur in iteration_point2(math, sharpen, in_to_sharpen, blur_to_sharpen) {
            // Sharpen
            let pix_sharpen = input_image * (one + weight) + blur * (-weight);
            out.write(x, y, pix_sharpen);
        }

        for math, x, y, out, sharpen , blur, input_image in iteration_point3(math, out, sharpen, blur_to_mask, in_to_mask) {
            // Mask (Ratio)
            let absolute_value = abs(input_image - blur);
            let pix_mask_out = if (absolute_value < threshold) {input_image} else {sharpen};
            out.write(x, y, pix_mask_out);
        }
    }
    print_total_timing();

    // display results
    let input_image_acc = get_acc(input_image, |idx, val| { bitcast[&mut[pixel_t]](input_image.buf.data)(idx) = val }, |idx| bitcast[&[pixel_t]](input_image.buf.data)(idx));
    let out_acc = get_acc(out, |idx, val| { bitcast[&mut[pixel_t]](out.buf.data)(idx) = val }, |idx| bitcast[&[pixel_t]](out.buf.data)(idx));

    let roi_x = 0;
    let roi_y = 0;
    display_image(input_image_acc, roi_x, roi_y);
    display_image(out_acc, roi_x, roi_y);

    release(input_image.buf);
    release(out.buf);
    0
}
