fn compute_config(range_mask: (int, int), unroll: int) -> ((int, int, int), (int, int)) {
    fn compute_config_y (y_max: int) -> int {
        if range_mask(1) > 1 { return(y_max) }
        for n in range(1, y_max+1) {
            if n * unroll >= range_mask(1) { return(n) }
        }
        y_max
    }

    let config_y_max = 4;   // must be 1 for CPU on Mac OS X
    let config_x_max = 128; // use 256 for AMD GPUs; 512 for Intel MIC

    let threads_y = compute_config_y(config_y_max);
    let threads_x = config_x_max / threads_y;

    let blocks_x = round_up(range_mask(0), threads_x) / threads_x;
    let blocks_y = round_up(range_mask(1), threads_y) / threads_y;

    ((threads_x, threads_y, 1), (blocks_x, blocks_y))
}

fn iteration_point(out: Img,
                   body: fn(int, int, Acc) -> ()
                  ) -> () {
    let unroll = 1;
    let grid   = (out.width, out.height/unroll, 1);
    let block  = (128, 1, 1);

    acc(acc_dev(), grid, block, || @{
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let out_acc = get_acc(out, 10);

        for i in range(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), out_acc);
        }
    });
}

fn iteration(out: Img, arr: Img, mask: Mask,
             body: fn(int, int, Acc, Acc, Mask) -> ()
            ) -> () {
    let unroll = 1;
    let config = compute_config((mask.size_x/2, mask.size_y/2), unroll);
    let grid   = (out.width, out.height/unroll, 1);
    let block  = config(0);

    acc(acc_dev(), grid, block, || @{
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let arr_acc = get_acc(arr, 10);
        let out_acc = get_acc(out, 10);

        for i in range(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), out_acc, arr_acc, mask);
        }
    });
}

fn iteration_bounds(out: Img, arr: Img, mask: Mask,
                    body: fn(int, int, Acc, Acc, Mask) -> ()
                   ) -> () {
    // compute the number of blocks required for boundary handling
    let unroll = 1;
    let config = compute_config((mask.size_x/2, mask.size_y/2), unroll);
    let block  = config(0);
    let bx     = config(1)(0);
    let by     = config(1)(1);

    // define if we want to generate one big kernel or multiple kernels
    let big_kernel = false;

    if big_kernel {
        let grid = (out.width, out.height/unroll, 1);

        acc(acc_dev(), grid, block, || @{
            let bid_x = acc_bidx();
            let bid_y = acc_bidy();
            let bdim_y = acc_bdimy();
            let gdim_x = acc_gdimx();
            let gdim_y = acc_gdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

            // 0 --- 1 --- 2
            // 3 --- 4 --- 5
            // 6 --- 7 --- 8
            if bid_x < bx && bid_y < by @{                  // top-left: 0
                let arr_acc = get_acc(arr, 0);
                let out_acc = get_acc(out, 0);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            if bid_x >= gdim_x-bx && bid_y < by @{          // top-right: 2
                let arr_acc = get_acc(arr, 2);
                let out_acc = get_acc(out, 2);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            if bid_y < by @{                                // top: 1
                let arr_acc = get_acc(arr, 1);
                let out_acc = get_acc(out, 1);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            if bid_y >= gdim_y-by && bid_x < bx @{          // bottom-left: 6
                let arr_acc = get_acc(arr, 6);
                let out_acc = get_acc(out, 6);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            if bid_y >= gdim_y-by && bid_x >= gdim_x-bx @{  // bottom-right: 8
                let arr_acc = get_acc(arr, 8);
                let out_acc = get_acc(out, 8);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            if bid_y >= gdim_y-by @{                        // bottom: 7
                let arr_acc = get_acc(arr, 7);
                let out_acc = get_acc(out, 7);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            if bid_x >= gdim_x-bx @{                        // right: 5
                let arr_acc = get_acc(arr, 5);
                let out_acc = get_acc(out, 5);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            if bid_x < bx @{                                // left: 3
                let arr_acc = get_acc(arr, 3);
                let out_acc = get_acc(out, 3);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
                return()
            }
            {                                               // center: 4
                let arr_acc = get_acc(arr, 4);
                let out_acc = get_acc(out, 4);
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, arr_acc, mask);
                }
            }
        });
    } else {
        // bounds account for unroll factor
        let hu = out.height/unroll;
        let Bounds2D = [
            (0                      , bx*block(0)            , 0               , by*block(1)),
            (bx*block(0)            , out.width - bx*block(0), 0               , by*block(1)),
            (out.width - bx*block(0), out.width              , 0               , by*block(1)),

            (0                      , bx*block(0)            , by*block(1)     , hu - by*block(1)),
            (bx*block(0)            , out.width - bx*block(0), by*block(1)     , hu - by*block(1)),
            (out.width - bx*block(0), out.width              , by*block(1)     , hu - by*block(1)),

            (0                      , bx*block(0)            , hu - by*block(1), hu),
            (bx*block(0)            , out.width - bx*block(0), hu - by*block(1), hu),
            (out.width - bx*block(0), out.width              , hu - by*block(1), hu)
        ];

        for region in @range(0, 9) {
            let bounds = Bounds2D(region);
            let grid   = (bounds(1) - bounds(0), bounds(3) - bounds(2), 1);

            let arr_acc = get_acc(arr, region);
            let out_acc = get_acc(out, region);

            acc(acc_dev(), grid, block, || @{
                let gid_x = bounds(0) +
                            acc_tidx() + acc_bdimx() * acc_bidx();
                let gid_y = bounds(2) * unroll +
                            acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), out_acc, arr_acc, mask);
                }
            });
        }
    }
}

fn iteration_advanced(out: Img, arr: Img, mask: Mask,
                      body: fn(int, int, Acc, Acc, Mask) -> ()
                     ) -> () {
    let unroll = 1;
    let config = compute_config((mask.size_x/2, mask.size_y/2), unroll);
    let grid   = (out.width, out.height/unroll, 1);
    let block  = config(0);

    // compute number of steps required to stage data to shared memory
    let range_row = mask.size_x / 2;
    let range_col = mask.size_y / 2;
    let steps_x   = 2;
    let offset_y  = if (mask.size_y-1)%block(1)==0 { 0 } else { 1 };
    let steps_y   = unroll + (mask.size_y-1)/block(1) + offset_y;

    let dev = acc_dev();
    let gpu_arr = mmap(arr.data, dev, acc_tex(), 0, arr.width * arr.height);
    let gpu_out = mmap(out.data, dev, acc_mem(), 0, out.width * out.height);

    acc(dev, grid, block, || @{
        let tid_x = acc_tidx();
        let tid_y = acc_tidy();
        let bdim_x = acc_bdimx();
        let bdim_y = acc_bdimy();
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

        let spm_stride =          block(0) + 2 * range_row;
        let spm_height = unroll * block(1) + 2 * range_col;
        let mut spm = mmap(arr.data, dev, acc_spm(), 0, spm_stride * spm_height);
        let spm_img = Img { data : spm,     width : spm_stride, height : spm_height, stride : spm_stride };
        let out_img = Img { data : gpu_out, width : out.width,  height : out.height, stride : out.width };

        for y in range(0, steps_y) {
            let lid_y = tid_y             + y*bdim_y;
            let idx_y = gid_y - range_col + y*bdim_y;
            for x in range(0, steps_x) {
                let lid_x = tid_x + x*bdim_x;
                let idx_x = gid_x - range_row + x*bdim_x;

                if lid_x < spm_stride && lid_y < spm_height @{
                    let gpu_img = Img { data : gpu_arr, width : arr.width, height : arr.height, stride : arr.stride };
                    let gpu_acc = get_acc(gpu_img, 10); // TODO: set region!
                    spm(lid_y * spm_stride + lid_x) = get_pixel(gpu_acc, idx_x, idx_y, clamp_lower, clamp_upper);
                }
            }
        }

        acc_barrier();

        for i in range(0, unroll) {
            // index space: block
            let out_acc = Acc { img : out_img, roi : RoI { o_x : 0, o_y : 0, width : out_img.width, height : out_img.height },
                                               rox : acc_bdimx() * acc_bidx(), roy : acc_bdimy() * acc_bidy() * unroll + i * bdim_y, region : 4 /* no bh */ };
            let arr_acc = Acc { img : spm_img, roi : RoI { o_x : 0, o_y : 0, width : spm_img.width, height : spm_img.height },
                                               rox : range_row,                roy : range_col + i * bdim_y,                         region : 4 /* no bh */ };
            body(tid_x, tid_y, out_acc, arr_acc, mask);
        }
    });

    munmap(gpu_arr);
    munmap(gpu_out);
}

fn iteration_sep(out: Img, arr: Img, mask_row: MaskSep, mask_col: MaskSep,
                 body: fn(int, int, Acc, Acc, MaskSep, bool) -> ()
                ) -> () {
    let unroll = 1;
    let grid   = (out.width, out.height/unroll, 1);
    let config_row = compute_config((mask_row.size/2, 1), unroll);
    let config_col = compute_config((1, mask_col.size/2), unroll);
    let block_row  = config_row(0);
    let block_col  = config_col(0);

    // allocate temporary array
    let tmp = Img { data : ~[out.width*out.height:float], width : out.width, height : out.height, stride : out.width };
    $init_zero(tmp.data, tmp.stride, tmp.height);

    let dev = acc_dev();
    let gpu_arr_ro = Img { data : mmap(arr.data, dev, acc_tex(), 0, arr.width * arr.height), width : arr.width, height : arr.height, stride : arr.stride };
    let gpu_tmp_ro = Img { data : mmap(tmp.data, dev, acc_tex(), 0, tmp.width * tmp.height), width : tmp.width, height : tmp.height, stride : tmp.stride };
    let gpu_tmp_wo = Img { data : mmap(tmp.data, dev, acc_mem(), 0, tmp.width * tmp.height), width : tmp.width, height : tmp.height, stride : tmp.stride };
    let gpu_out_wo = Img { data : mmap(out.data, dev, acc_mem(), 0, out.width * out.height), width : out.width, height : out.height, stride : out.stride };

    acc(dev, grid, block_col, || @{
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let is_row = false;

        let arr_acc = get_acc(gpu_arr_ro, 10);
        let tmp_acc = get_acc(gpu_tmp_wo, 10);

        for i in range(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), tmp_acc, arr_acc, mask_col, is_row);
        }
    });
    acc(dev, grid, block_row, || @{
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let is_row = true;

        let tmp_acc = get_acc(gpu_tmp_ro, 10);
        let out_acc = get_acc(gpu_out_wo, 10);

        for i in range(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), out_acc, tmp_acc, mask_row, is_row);
        }
    });

    munmap(gpu_arr_ro.data);
    munmap(gpu_tmp_ro.data);
    munmap(gpu_tmp_wo.data);
    munmap(gpu_out_wo.data);
}

fn iteration_sep_bounds(out: Img, arr: Img, mask_row: MaskSep, mask_col: MaskSep,
                        body: fn(int, int, Acc, Acc, MaskSep, bool) -> ()
                       ) -> () {
    // compute the number of blocks required for boundary handling
    let unroll = 1;
    let config_row = compute_config((mask_row.size/2, 1), unroll);
    let config_col = compute_config((1, mask_col.size/2), unroll);
    let block_row  = config_row(0);
    let block_col  = config_col(0);
    let bx         = config_row(1)(0);
    let by         = config_col(1)(1);

    let Region2DCol = [ 1, 4, 7 ];
    let Region2DRow = [ 3, 4, 5 ];

    // allocate temporary array
    let tmp = Img { data : ~[out.width*out.height:float], width : out.width, height : out.height, stride : out.width };
    $init_zero(tmp.data, tmp.stride, tmp.height);

    let dev = acc_dev();
    let gpu_arr_ro = Img { data : mmap(arr.data, dev, acc_tex(), 0, arr.width * arr.height), width : arr.width, height : arr.height, stride : arr.stride };
    let gpu_tmp_ro = Img { data : mmap(tmp.data, dev, acc_tex(), 0, tmp.width * tmp.height), width : tmp.width, height : tmp.height, stride : tmp.stride };
    let gpu_tmp_wo = Img { data : mmap(tmp.data, dev, acc_mem(), 0, tmp.width * tmp.height), width : tmp.width, height : tmp.height, stride : tmp.stride };
    let gpu_out_wo = Img { data : mmap(out.data, dev, acc_mem(), 0, out.width * out.height), width : out.width, height : out.height, stride : out.stride };

    // define if we want to generate one big kernel or multiple kernels
    let big_kernel = false;

    if big_kernel {
        let grid = (out.width, out.height/unroll, 1);

        // column component
        acc(dev, grid, block_col, || @{
            let bid_y = acc_bidy();
            let bdim_y = acc_bdimy();
            let gdim_y = acc_gdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
            let is_row = false;

            if bid_y < by @{                                // top: 1
                let arr_acc = get_acc(gpu_arr_ro, Region2DCol(0));
                let tmp_acc = get_acc(gpu_tmp_wo, Region2DCol(0));
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, tmp_acc, arr_acc, mask_col, is_row);
                }
                return()
            }
            if bid_y >= gdim_y-by @{                        // bottom: 7
                let arr_acc = get_acc(gpu_arr_ro, Region2DCol(2));
                let tmp_acc = get_acc(gpu_tmp_wo, Region2DCol(2));
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, tmp_acc, arr_acc, mask_col, is_row);
                }
                return()
            }
            @{                                              // center: 4
                let arr_acc = get_acc(gpu_arr_ro, Region2DCol(1));
                let tmp_acc = get_acc(gpu_tmp_wo, Region2DCol(1));
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, tmp_acc, arr_acc, mask_col, is_row);
                }
            }
        });

        // row component
        acc(acc_dev(), grid, block_row, || @{
            let bid_x = acc_bidx();
            let bdim_y = acc_bdimy();
            let gdim_x = acc_gdimx();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
            let is_row = true;

            if bid_x < bx @{                                // left: 3
                let tmp_acc = get_acc(gpu_tmp_ro, Region2DRow(0));
                let out_acc = get_acc(gpu_out_wo, Region2DRow(0));
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, tmp_acc, mask_row, is_row);
                }
                return()
            }
            if bid_x >= gdim_x-bx @{                        // right: 5
                let tmp_acc = get_acc(gpu_tmp_ro, Region2DRow(2));
                let out_acc = get_acc(gpu_out_wo, Region2DRow(2));
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, tmp_acc, mask_row, is_row);
                }
                return()
            }
            @{                                               // center: 4
                let tmp_acc = get_acc(gpu_tmp_ro, Region2DRow(1));
                let out_acc = get_acc(gpu_out_wo, Region2DRow(1));
                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, out_acc, tmp_acc, mask_row, is_row);
                }
            }
        });
    } else {
        let hu = out.height / unroll;
        let Bounds2DCol = [
            (0                   , by*block_col(1)),
            (by*block_col(1)     , hu - by*block_col(1)),
            (hu - by*block_col(1), hu)
        ];

        for iter in @range(0, 3) {
            let region = Region2DCol(iter);
            let bounds = Bounds2DCol(iter);
            let grid   = (out.width, bounds(1) - bounds(0), 1);

            let arr_acc = get_acc(gpu_arr_ro, region);
            let tmp_acc = get_acc(gpu_tmp_wo, region);

            acc(dev, grid, block_col, || @{
                let gid_x = acc_gidx();
                let gid_y = bounds(0) * unroll +
                            acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
                let is_row = false;

                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), tmp_acc, arr_acc, mask_col, is_row);
                }
            });
        }

        let Bounds2DRow = [
            (0                          , bx*block_row(0)),
            (bx*block_row(0)            , out.width - bx*block_row(0)),
            (out.width - bx*block_row(0), out.width)
        ];

        for iter in @range(0, 3) {
            let region = Region2DRow(iter);
            let bounds = Bounds2DRow(iter);
            let grid   = (bounds(1) - bounds(0), out.height/unroll, 1);

            let tmp_acc = get_acc(gpu_tmp_ro, region);
            let out_acc = get_acc(gpu_out_wo, region);

            acc(acc_dev(), grid, block_row, || @{
                let gid_x = bounds(0) + acc_gidx();
                let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
                let is_row = true;

                for i in range(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), out_acc, tmp_acc, mask_row, is_row);
                }
            });
        }
    }

    munmap(gpu_arr_ro.data);
    munmap(gpu_tmp_ro.data);
    munmap(gpu_tmp_wo.data);
    munmap(gpu_out_wo.data);
}

fn iteration_sep_advanced(out: Img, arr: Img, mask_row: MaskSep, mask_col: MaskSep,
                          body: fn(int, int, Acc, Acc, MaskSep, bool) -> ()
                         ) -> () {
    let unroll = 1;
    let config = compute_config((mask_row.size/2, mask_col.size/2), unroll);
    let grid   = (out.width, out.height/unroll, 1);
    let block  = config(0);

    // compute number of steps required to stage data to shared memory
    let range_row = mask_row.size / 2;
    let range_col = mask_col.size / 2;
    let offset_y  = if (mask_col.size-1)%block(1)==0 { 0 } else { 1 };
    let steps_x   = 2;
    let steps_y   = unroll + (mask_col.size-1)/block(1) + offset_y;

    let dev = acc_dev();
    let gpu_arr = mmap(arr.data, dev, acc_tex(), 0, arr.width * out.height);
    let gpu_out = mmap(out.data, dev, acc_mem(), 0, out.width * out.height);

    acc(dev, grid, block, || @{
        let tid_x = acc_tidx();
        let tid_y = acc_tidy();
        let bdim_x = acc_bdimx();
        let bdim_y = acc_bdimy();
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

        let spm_stride     =          block(0) + 2 * range_row;
        let spm_height_col = unroll * block(1) + 2 * range_col;
        let spm_height_row = unroll * block(1);
        let mut spm_col = mmap(arr.data, dev, acc_spm(), 0, spm_stride * spm_height_col);
        let     spm_row = mmap(arr.data, dev, acc_spm(), 0, spm_stride * spm_height_row);
        let spmr_img = Img { data : spm_row, width : spm_stride,  height : spm_height_row, stride : spm_stride };
        let spmc_img = Img { data : spm_col, width : spm_stride,  height : spm_height_col, stride : spm_stride };
        let out_img  = Img { data : gpu_out, width : out.width,   height : out.height,     stride : out.width };

        for y in range(0, steps_y) {
            let lid_y = tid_y             + y*bdim_y;
            let idx_y = gid_y - range_col + y*bdim_y;
            for x in range(0, steps_x) {
                let lid_x = tid_x + x*bdim_x;
                let idx_x = gid_x - range_row + x*bdim_x;

                if lid_x < spm_stride && lid_y < spm_height_col @{
                    let gpu_img = Img { data : gpu_arr, width : arr.width, height : arr.height, stride : arr.stride };
                    let gpu_acc = get_acc(gpu_img, 10); // TODO: set region!
                    spm_col(lid_y * spm_stride + lid_x) = get_pixel(gpu_acc, idx_x, idx_y, clamp_lower, clamp_upper);
                }
            }
        }

        acc_barrier();
        let region = 4; // center: no bh

        for i in range(0, unroll) {
            let is_row = false;
            {
                // index space: block
                let out_acc = Acc { img : spmr_img, roi : RoI { o_x : 0, o_y : 0, width : spmr_img.width, height : spmr_img.height }, rox : range_row, roy :             i * bdim_y, region : region };
                let arr_acc = Acc { img : spmc_img, roi : RoI { o_x : 0, o_y : 0, width : spmc_img.width, height : spmc_img.height }, rox : range_row, roy : range_col + i * bdim_y, region : region };
                body(tid_x, tid_y, out_acc, arr_acc, mask_col, is_row);
            }
            if tid_x < range_row @{
                // left halo
                let out_acc = Acc { img : spmr_img, roi : RoI { o_x : 0, o_y : 0, width : spmr_img.width, height : spmr_img.height }, rox : 0, roy :             i * bdim_y, region : region };
                let arr_acc = Acc { img : spmc_img, roi : RoI { o_x : 0, o_y : 0, width : spmc_img.width, height : spmc_img.height }, rox : 0, roy : range_col + i * bdim_y, region : region };
                body(tid_x, tid_y, out_acc, arr_acc, mask_col, is_row);
            }
            if tid_x >= bdim_x-range_row @{
                // right halo
                let out_acc = Acc { img : spmr_img, roi : RoI { o_x : 0, o_y : 0, width : spmr_img.width, height : spmr_img.height }, rox : range_row + range_row, roy :             i * bdim_y, region : region };
                let arr_acc = Acc { img : spmc_img, roi : RoI { o_x : 0, o_y : 0, width : spmc_img.width, height : spmc_img.height }, rox : range_row + range_row, roy : range_col + i * bdim_y, region : region };
                body(tid_x, tid_y, out_acc, arr_acc, mask_col, is_row);
            }
        }

        acc_barrier();

        for i in range(0, unroll) {
            // index space: block
            let is_row = true;
            let out_acc = Acc { img : out_img,  roi : RoI { o_x : 0, o_y : 0, width : out_img.width,  height : out_img.height },
                                                rox : acc_bdimx() * acc_bidx(), roy : acc_bdimy() * acc_bidy() * unroll + i * bdim_y, region : region };
            let arr_acc = Acc { img : spmr_img, roi : RoI { o_x : 0, o_y : 0, width : spmr_img.width, height : spmr_img.height },
                                                rox : range_row,                roy : i * bdim_y,                                     region : region };
            body(tid_x, tid_y, out_acc, arr_acc, mask_row, is_row);
        }
    });

    munmap(gpu_arr);
    munmap(gpu_out);
}

fn iteration_multi(width: int, height: int,
                   out: &[float], arr: &[float],
                   body: fn(int, int, &[float], &[float]) -> ()
                  ) -> () {
    let mut ids : [int * 2];
    let mut map_arr : [&[f32] * 2];
    let mut map_out : [&[f32] * 2];

    for dev in @range(0, 2) {
        let height_dev = height / 2;
        let mem_size_dev = width * height_dev;
        let offset = dev * mem_size_dev;
        let grid  = (width, height_dev, 1);
        let block = (128, 1, 1);

        map_arr(dev) = mmap(arr, dev, acc_tex(), offset, mem_size_dev);
        map_out(dev) = mmap(out, dev, acc_mem(), offset, mem_size_dev);

        ids(dev) = spawn(|| -> () @{
            acc(dev, grid, block, || @{
                body(acc_gidx(), acc_gidy(), map_out(dev), map_arr(dev));
            });
        });
    }

    for dev in range(0, 2) {
        sync(ids(dev));
        munmap(map_arr(dev));
        munmap(map_out(dev));
    }
}
