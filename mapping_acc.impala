fn iteration(width : int, height : int,
             arr : &[float], out : &[float],
             body : fn(int, int, &[float], &[float]) -> ()
            ) -> () {
    let unroll = 1;
    let dim = (width, height/unroll, 1);
    let block = (128, 1, 1);

    let dev = 1;
    acc(dev, dim, block, || -> () {
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

        for i in @iterate(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), arr, out);
        }
    });
}

fn iteration_bounds(width : int, height : int, ksize : int,
                    arr : &[float], out : &[float],
                    body : fn(int, int, &[float], &[float], int) -> ()
                   ) -> () {
    // define the unroll factor
    let unroll = 1;
    // define if we want to generate one big kernel or multiple kernels
    let big_kernel = false;


    // compute the number of blocks required for boundary handling
    let bbh = if ((ksize/2) % unroll==0) {
                (ksize/2) / unroll
              } else {
                (ksize/2) / unroll + 1
              };
    let block = (128, bbh, 1);

    let dev = 1;
    if big_kernel {
        let dim = (width, height/unroll, 1);

        acc(dev, dim, block, || -> () {
            let bid_x = acc_bidx();
            let bid_y = acc_bidy();
            let bdim_y = acc_bdimy();
            let gdim_x = acc_gdimx();
            let gdim_y = acc_gdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

            // 0 --- 1 --- 2
            // 3 --- 4 --- 5
            // 6 --- 7 --- 8
            if (bid_x < 1 && bid_y < 1) {                   // top-left: 0
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 0);
                }
                return()
            }
            if (bid_x >= gdim_x-1 && bid_y < 1) {           // top-right: 2
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 2);
                }
                return()
            }
            if (bid_y < 1) {                                // top: 1
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 1);
                }
                return()
            }
            if (bid_y >= gdim_y-1 && bid_x < 1) {           // bottom-left: 6
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 6);
                }
                return()
            }
            if (bid_y >= gdim_y-1 && bid_x >= gdim_x-1) {   // bottom-right: 8
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 8);
                }
                return()
            }
            if (bid_y >= gdim_y-1) {                        // bottom: 7
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 7);
                }
                return()
            }
            if (bid_x >= gdim_x-1) {                        // right: 5
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 5);
                }
                return()
            }
            if (bid_x < 1) {                                // left: 3
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, out, 3);
                }
                return()
            }
            // no border handling
            for i in @iterate(0, unroll) {
                body(gid_x, gid_y + i * bdim_y, arr, out, 4);
            }
        });
    } else {
        // bounds account for unroll factor
        let hu = height/unroll;
        let Bounds2D = [
            (0               , block(0)        , 0            , block(1)),
            (block(0)        , width - block(0), 0            , block(1)),
            (width - block(0), width           , 0            , block(1)),

            (0               , block(0)        , block(1)     , hu - block(1)),
            (block(0)        , width - block(0), block(1)     , hu - block(1)),
            (width - block(0), width           , block(1)     , hu - block(1)),

            (0               , block(0)        , hu - block(1), hu),
            (block(0)        , width - block(0), hu - block(1), hu),
            (width - block(0), width           , hu - block(1), hu)
        ];

        for region in @iterate(0, 9) {
            let bounds = Bounds2D(region);
            let dim = (bounds(1) - bounds(0), bounds(3) - bounds(2), 1);

            acc(dev, dim, block, || -> () {
                let gid_x = bounds(0) +
                            acc_tidx() + acc_bdimx() * acc_bidx();
                let gid_y = bounds(2) * unroll +
                            acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), arr, out, region);
                }
            });
        }
    }
}

fn iteration_advanced(width : int, height : int, ksize : int,
                      arr : &[float], out : &[float],
                      body : fn(int, int, &[float], &[float], int, int, int) -> ()
                     ) -> () {
    for unroll in @iterate_times_two(1, 16+1) {
        // compute the number of blocks required for boundary handling
        let bbh = if ((ksize/2) % unroll==0) {
                    (ksize/2) / unroll
                  } else {
                    (ksize/2) / unroll + 1
                  };

        let dim = (width, height/unroll, 1);
        let block = (128/bbh, bbh, 1);

        // compute number of steps required to stage data to shared memory
        let ksize2 = ksize / 2;
        let steps_x = 2;
        let offset_y = if ksize%block(1)==0 { 0 } else { 1 };
        let steps_y = unroll + ksize/block(1) + offset_y;

        // 0 -> generic
        // 1 -> global
        // 2 -> texture
        // 3 -> shared
        // 4 -> constant
        let dev = 1;
        let gpu_arr = mmap(arr, dev, acc_tex(), (0, 0, 0), (width, height, 1));
        let gpu_out = mmap(out, dev, 1,         (0, 0, 0), (width, height, 1));

        acc(dev, dim, block, || -> () {
            let tid_x = acc_tidx();
            let tid_y = acc_tidy();
            let bdim_x = acc_bdimx();
            let bdim_y = acc_bdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

            let mut smem = mmap(arr, dev, 3, (0, 0, 0), (block(0) + 2*ksize2, block(1)*unroll + 2*ksize2, 1));
            let lstride = block(0) + 2*ksize2;

            for y in @range(0, steps_y) {
                let lid_y = tid_y + y*bdim_y;
                let mut idx_y = gid_y - ksize2 + y*bdim_y;
                idx_y = clamp_upper(idx_y, height);
                idx_y = clamp_lower(idx_y, 0);
                for x in @range(0, steps_x) {
                    let lid_x = tid_x + x*bdim_x;
                    let mut idx_x = gid_x - ksize2 + x*bdim_x;
                    idx_x = clamp_lower(idx_x, 0);
                    idx_x = clamp_upper(idx_x, width);

                    if (lid_x < block(0) + 2*ksize2 &&
                        lid_y < block(1)*unroll + 2*ksize2) {
                        smem(lid_y * lstride + lid_x) = gpu_arr(idx_y * width + idx_x);
                    }
                }
            }

            acc_barrier();

            for i in @iterate(0, unroll) {
                body(gid_x, gid_y + i * bdim_y, smem, gpu_out, tid_x+ksize2, tid_y+ksize2 + i * bdim_y, lstride);
            }
        });
    }
}

fn iteration_sep(width : int, height : int,
                 arr : &[float], out : &[float],
                 body : fn(int, int, &[float], &[float], bool) -> ()
                ) -> () {
    let unroll = 1;
    let dim = (width, height/unroll, 1);
    let block = (128, 1, 1);

    // allocate temporary array
    let tmp = ~[width*height:float];
    init_zero(tmp, $width, $height);

    let dev = 1;
    acc(dev, dim, block, || -> () {
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let is_row = false;

        for i in @iterate(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), arr, tmp, is_row);
        }
    });
    acc(dev, dim, block, || -> () {
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let is_row = true;

        for i in @iterate(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), tmp, out, is_row);
        }
    });
}

fn iteration_sep_bounds(width : int, height : int, ksize : int,
                        arr : &[float], out : &[float],
                        body : fn(int, int, &[float], &[float], bool, int) -> ()
                       ) -> () {
    // define the unroll factor
    let unroll = 1;
    // define if we want to generate one big kernel or multiple kernels
    let big_kernel = false;


    // compute the number of blocks required for boundary handling
    let bbh = if ((ksize/2) % unroll==0) {
                (ksize/2) / unroll
              } else {
                (ksize/2) / unroll + 1
              };
    let block = (128, bbh, 1);

    // allocate temporary array
    let tmp = ~[width*height:float];
    init_zero(tmp, $width, $height);

    let dev = 1;
    if big_kernel {
        let dim = (width, height/unroll, 1);

        // column component
        acc(dev, dim, block, || -> () {
            let bid_y = acc_bidy();
            let bdim_y = acc_bdimy();
            let gdim_y = acc_gdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
            let is_row = false;

            // 0
            // 1
            // 2
            if (bid_y < 1) {                                // top: 0
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, tmp, is_row, 0);
                }
                return()
            }
            if (bid_y >= gdim_y-1) {                        // bottom: 2
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, arr, tmp, is_row, 2);
                }
                return()
            }
            // no border handling
            for i in @iterate(0, unroll) {
                body(gid_x, gid_y + i * bdim_y, arr, tmp, is_row, 1);
            }
        });

        // row component
        acc(dev, dim, block, || -> () {
            let bid_x = acc_bidx();
            let bdim_y = acc_bdimy();
            let gdim_x = acc_gdimx();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
            let is_row = true;

            // 0 --- 1 --- 2
            if (bid_x < 1) {                                // left: 0
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, tmp, out, is_row, 0);
                }
                return()
            }
            if (bid_x >= gdim_x-1) {                        // right: 2
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, tmp, out, is_row, 2);
                }
                return()
            }
            // no border handling
            for i in @iterate(0, unroll) {
                body(gid_x, gid_y + i * bdim_y, tmp, out, is_row, 1);
            }
        });
    } else {
        let hu = height / unroll;
        let Bounds2DCol = [
            (0               , block(1)),
            (block(1)        , hu - block(1)),
            (hu - block(1)   , hu)
        ];

        for region in iterate(0, 3) {
            let bounds = Bounds2DCol(region);
            let dim = (width, bounds(1) - bounds(0), 1);

            acc(dev, dim, block, || -> () {
                let gid_x = acc_gidx();
                let gid_y = bounds(0) * unroll +
                            acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
                let is_row = false;

                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), arr, tmp, is_row, region);
                }
            });
        }

        let Bounds2DRow = [
            (0               , block(0)),
            (block(0)        , width - block(0)),
            (width - block(0), width)
        ];

        for region in iterate(0, 3) {
            let bounds = Bounds2DRow(region);
            let dim = (bounds(1) - bounds(0), height/unroll, 1);

            acc(dev, dim, block, || -> () {
                let gid_x = bounds(0) + acc_gidx();
                let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
                let is_row = true;

                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), tmp, out, is_row, region);
                }
            });
        }
    }
}

fn iteration_sep_advanced(width : int, height : int, ksize : int,
                          arr : &[float], out : &[float],
                          body : fn(int, int, &[float], &[float], int, int, int, int, bool) -> ()
                         ) -> () {
    for unroll in iterate_times_two(1, 16+1) {
        // compute the number of blocks required for boundary handling
        let bbh = if ((ksize/2) % unroll==0) {
                    (ksize/2) / unroll
                  } else {
                    (ksize/2) / unroll + 1
                  };

        let dim = (width, height/unroll, 1);
        let block = (128/bbh, bbh, 1);

        // compute number of steps required to stage data to shared memory
        let ksize2 = ksize / 2;
        let steps_x = 2;
        let offset_y = if ksize%block(1)==0 { 0 } else { 1 };
        let steps_y = unroll + ksize/block(1) + offset_y;

        // 0 -> generic
        // 1 -> global
        // 2 -> texture
        // 3 -> shared
        // 4 -> constant
        let dev = 1;
        let gpu_arr = mmap(arr, dev, acc_tex(), (0, 0, 0), (width, height, 1));
        let gpu_out = mmap(out, dev, 1,         (0, 0, 0), (width, height, 1));

        acc(dev, dim, block, || -> () {
            let tid_x = acc_tidx();
            let tid_y = acc_tidy();
            let bdim_x = acc_bdimx();
            let bdim_y = acc_bdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

            let mut smem_col = mmap(arr, dev, 3, (0, 0, 0), (block(0) + 2*ksize2, block(1)*unroll + 2*ksize2, 1));
            let     smem_row = mmap(arr, dev, 3, (0, 0, 0), (block(0) + 2*ksize2, block(1)*unroll, 1));
            let lstride = block(0) + 2*ksize2;

            for y in @range(0, steps_y) {
                let lid_y = tid_y + y*bdim_y;
                let mut idx_y = gid_y - ksize2 + y*bdim_y;
                idx_y = clamp_upper(idx_y, height);
                idx_y = clamp_lower(idx_y, 0);
                for x in @range(0, steps_x) {
                    let lid_x = tid_x + x*bdim_x;
                    let mut idx_x = gid_x - ksize2 + x*bdim_x;
                    idx_x = clamp_lower(idx_x, 0);
                    idx_x = clamp_upper(idx_x, width);

                    if (lid_x < lstride &&
                        lid_y < block(1)*unroll + 2*ksize2) {
                        smem_col(lid_y * lstride + lid_x) = gpu_arr(idx_y * width + idx_x);
                    }
                }
            }

            acc_barrier();

            for i in @iterate(0, unroll) {
                let is_row = false;
                body(tid_x+ksize2, tid_y + i * bdim_y, smem_col, smem_row, tid_x+ksize2, tid_y+ksize2 + i * bdim_y, lstride, lstride, is_row);
                if (tid_x < ksize2) {
                    @body(tid_x, tid_y + i * bdim_y, smem_col, smem_row, tid_x, tid_y+ksize2 + i * bdim_y, lstride, lstride, is_row);
                }
                if (tid_x >= bdim_x-ksize2) {
                    @body(tid_x+ksize2+ksize2, tid_y + i * bdim_y, smem_col, smem_row, tid_x+ksize2+ksize2, tid_y+ksize2 + i * bdim_y, lstride, lstride, is_row);
                }
            }

            acc_barrier();

            for i in @iterate(0, unroll) {
                let is_row = true;
                body(gid_x, gid_y + i * bdim_y, smem_row, gpu_out, tid_x+ksize2, tid_y + i * bdim_y, lstride, width, is_row);
            }
        });
    }
}

fn iteration_multi(width : int, height : int,
                   arr : &[float], out : &[float],
                   body : fn(int, int, &[float], &[float]) -> ()
                  ) -> () {
    let dim = (width, height/2, 1);
    let block = (128, 1, 1);

    let dev_gpu1 = 1;
    let map_arr_GPU1 = mmap(arr, dev_gpu1, 1, (0, 0, 0), (width, height/2, 1));
    let map_out_GPU1 = mmap(out, dev_gpu1, 1, (0, 0, 0), (width, height/2, 1));

    acc(dev_gpu1, dim, block, || -> () {
        let gid_x = acc_gidx();
        let gid_y = acc_gidy();

        @body(gid_x, gid_y, map_arr_GPU1, map_out_GPU1);
    });

    let dev_gpu2 = 2;
    let map_arr_GPU2 = mmap(arr, dev_gpu2, 1, (0, height/2, 0), (width, height/2, 1));
    let map_out_GPU2 = mmap(out, dev_gpu2, 1, (0, height/2, 0), (width, height/2, 1));

    acc(dev_gpu2, dim, block, || -> () {
        let gid_x = acc_gidx();
        let gid_y = acc_gidy();

        @body(gid_x, gid_y, map_arr_GPU2, map_out_GPU2);
    });
}
