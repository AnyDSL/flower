fn compute_config(m_range : (int, int), unroll : int) -> ((int, int, int), (int, int)) {
    fn compute_config_y (y_max : int) -> int {
        if m_range(1) > 1 { return(y_max) }
        for n in range(1, y_max+1) {
            if n * unroll >= m_range(1) { return(n) }
        }
        y_max
    }

    let config_y_max = 4;   // must be 1 for CPU on Mac OS X
    let config_x_max = 128; // use 256 for AMD GPUs

    let threads_y = compute_config_y(config_y_max);
    let threads_x = config_x_max / threads_y;

    let blocks_x = round_up(m_range(0), threads_x) / threads_x;
    let blocks_y = round_up(m_range(1), threads_y) / threads_y;

    ((threads_x, threads_y, 1), (blocks_x, blocks_y))
}

fn iteration(mask : Mask, arr : Img, out : Img,
             body : fn(int, int, Mask, Acc, Acc) -> ()
            ) -> () {
    let unroll = 1;
    let config = @compute_config((mask.size_x/2, mask.size_y/2), unroll);
    let dim = (out.width, out.height/unroll, 1);
    let block = config(0);

    acc(acc_dev(), dim, block, || {
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

        let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 10 };
        let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 10 };

        for i in @iterate(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), mask, arr_acc, out_acc);
        }
    });
}

fn iteration_bounds(mask : Mask, arr : Img, out : Img,
                    body : fn(int, int, Mask, Acc, Acc) -> ()
                   ) -> () {
    // compute the number of blocks required for boundary handling
    let unroll = 1;
    let config = @compute_config((mask.size_x/2, mask.size_y/2), unroll);
    let block = config(0);
    let bx = config(1)(0);
    let by = config(1)(1);

    // define if we want to generate one big kernel or multiple kernels
    let big_kernel = false;

    if big_kernel {
        let dim = (out.width, out.height/unroll, 1);

        acc(acc_dev(), dim, block, || {
            let bid_x = acc_bidx();
            let bid_y = acc_bidy();
            let bdim_y = acc_bdimy();
            let gdim_x = acc_gdimx();
            let gdim_y = acc_gdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

            // 0 --- 1 --- 2
            // 3 --- 4 --- 5
            // 6 --- 7 --- 8
            if (bid_x < bx && bid_y < by) {                 // top-left: 0
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 0 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 0 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            if (bid_x >= gdim_x-bx && bid_y < by) {         // top-right: 2
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 2 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 2 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            if (bid_y < by) {                               // top: 1
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 1 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 1 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            if (bid_y >= gdim_y-by && bid_x < bx) {         // bottom-left: 6
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 6 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 6 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            if (bid_y >= gdim_y-by && bid_x >= gdim_x-bx) { // bottom-right: 8
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 8 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 8 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            if (bid_y >= gdim_y-by) {                       // bottom: 7
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 7 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 7 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            if (bid_x >= gdim_x-bx) {                       // right: 5
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 5 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 5 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            if (bid_x < bx) {                               // left: 3
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 3 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 3 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
                }
                return()
            }
            // no border handling: 4
            let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 4 };
            let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 4 };
            for i in @iterate(0, unroll) {
                body(gid_x, gid_y + i * bdim_y, mask, arr_acc, out_acc);
            }
        });
    } else {
        // bounds account for unroll factor
        let hu = out.height/unroll;
        let Bounds2D = [
            (0                      , bx*block(0)            , 0               , by*block(1)),
            (bx*block(0)            , out.width - bx*block(0), 0               , by*block(1)),
            (out.width - bx*block(0), out.width              , 0               , by*block(1)),

            (0                      , bx*block(0)            , by*block(1)     , hu - by*block(1)),
            (bx*block(0)            , out.width - bx*block(0), by*block(1)     , hu - by*block(1)),
            (out.width - bx*block(0), out.width              , by*block(1)     , hu - by*block(1)),

            (0                      , bx*block(0)            , hu - by*block(1), hu),
            (bx*block(0)            , out.width - bx*block(0), hu - by*block(1), hu),
            (out.width - bx*block(0), out.width              , hu - by*block(1), hu)
        ];

        for region in @iterate(0, 9) {
            let bounds = Bounds2D(region);
            let dim = (bounds(1) - bounds(0), bounds(3) - bounds(2), 1);

            let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : region };
            let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : region };

            acc(acc_dev(), dim, block, || {
                let gid_x = bounds(0) +
                            acc_tidx() + acc_bdimx() * acc_bidx();
                let gid_y = bounds(2) * unroll +
                            acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), mask, arr_acc, out_acc);
                }
            });
        }
    }
}

fn iteration_advanced(mask : Mask, arr : Img, out : Img,
                      body : fn(int, int, Mask, Acc, Acc) -> ()
                     ) -> () {
    let unroll = 1;
    let config = @compute_config((mask.size_x/2, mask.size_y/2), unroll);
    let dim = (out.width, out.height/unroll, 1);
    let block = config(0);

    // compute number of steps required to stage data to shared memory
    let m_range_x = mask.size_x / 2;
    let m_range_y = mask.size_y / 2;
    let steps_x = 2;
    let offset_y = if (mask.size_y-1)%block(1)==0 { 0 } else { 1 };
    let steps_y = unroll + (mask.size_y-1)/block(1) + offset_y;

    // 0 -> generic
    // 1 -> global
    // 2 -> texture
    // 3 -> shared
    // 4 -> constant
    let dev = acc_dev();
    let gpu_arr = mmap(arr.data, dev, acc_tex(), 0, arr.width * arr.height);
    let gpu_out = mmap(out.data, dev, 1,         0, out.width * out.height);

    acc(dev, dim, block, || {
        let tid_x = acc_tidx();
        let tid_y = acc_tidy();
        let bdim_x = acc_bdimx();
        let bdim_y = acc_bdimy();
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

        let lstride = block(0) + 2 * m_range_x;
        let lheight = block(1) * unroll + 2 * m_range_y;
        let mut smem = mmap(arr.data, dev, 3, 0, lstride * lheight);
        let sma_img = Img { data : smem,    width : lstride,   height : lheight,    stride : lstride };
        let out_img = Img { data : gpu_out, width : out.width, height : out.height, stride : out.width };

        for y in @range(0, steps_y) {
            let lid_y = tid_y + y*bdim_y;
            let mut idx_y = gid_y - m_range_y + y*bdim_y;
            idx_y = clamp_lower(idx_y, 0);
            idx_y = clamp_upper(idx_y, arr.height);
            for x in @range(0, steps_x) {
                let lid_x = tid_x + x*bdim_x;
                let mut idx_x = gid_x - m_range_x + x*bdim_x;
                idx_x = clamp_lower(idx_x, 0);
                idx_x = clamp_upper(idx_x, arr.width);

                if (lid_x < lstride && lid_y < lheight) {
                    smem(lid_y * lstride + lid_x) = gpu_arr(idx_y * arr.width + idx_x);
                }
            }
        }

        acc_barrier();

        for i in @iterate(0, unroll) {
            // index space: block
            let out_acc = Acc { img : out_img, o_x : acc_bdimx() * acc_bidx(), o_y : acc_bdimy() * acc_bidy() * unroll + i * bdim_y, width : out_img.width, height : out_img.height, region : 4 /* no bh */};
            let arr_acc = Acc { img : sma_img, o_x : m_range_x,                o_y : m_range_y + i * bdim_y,                         width : sma_img.width, height : sma_img.height, region : 4 /* no bh */};
            body(tid_x, tid_y, mask, arr_acc, out_acc);
        }
    });
}

fn iteration_sep(mask : MaskSep, arr : Img, out : Img,
                 body : fn(int, int, MaskSep, Acc, Acc, bool) -> ()
                ) -> () {
    let unroll = 1;
    let config = @compute_config((mask.size/2, mask.size/2), unroll);
    let dim = (out.width, out.height/unroll, 1);
    let block = config(0);

    // allocate temporary array
    let tmp = Img { data : ~[out.width*out.height:float], width : out.width, height : out.height, stride : out.width };
    init_zero(tmp.data, $tmp.stride, $tmp.height);
    let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 10 };
    let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 10 };
    let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : 10 };

    let dev = acc_dev();
    acc(dev, dim, block, || {
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let is_row = false;

        for i in @iterate(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), mask, arr_acc, tmp_acc, is_row);
        }
    });
    acc(dev, dim, block, || {
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
        let is_row = true;

        for i in @iterate(0, unroll) {
            body(gid_x, gid_y + i * acc_bdimy(), mask, tmp_acc, out_acc, is_row);
        }
    });
}

fn iteration_sep_bounds(mask : MaskSep, arr : Img, out : Img,
                        body : fn(int, int, MaskSep, Acc, Acc, bool) -> ()
                       ) -> () {
    // compute the number of blocks required for boundary handling
    let unroll = 1;
    let config = @compute_config((mask.size/2, mask.size/2), unroll);
    let block = config(0);
    let bx = config(1)(0);
    let by = config(1)(1);

    // allocate temporary array
    let tmp = Img { data : ~[out.width*out.height:float], width : out.width, height : out.height, stride : out.width };
    init_zero(tmp.data, $tmp.stride, $tmp.height);

    // define if we want to generate one big kernel or multiple kernels
    let big_kernel = false;

    if big_kernel {
        let dim = (out.width, out.height/unroll, 1);

        // column component
        acc(acc_dev(), dim, block, || {
            let bid_y = acc_bidy();
            let bdim_y = acc_bdimy();
            let gdim_y = acc_gdimy();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
            let is_row = false;

            // 0
            // 1
            // 2
            if (bid_y < by) {                               // top: 0
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 0 };
                let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : 0 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, tmp_acc, is_row);
                }
                return()
            }
            if (bid_y >= gdim_y-by) {                       // bottom: 2
                let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 2 };
                let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : 2 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, arr_acc, tmp_acc, is_row);
                }
                return()
            }
            // no border handling: 1
            let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : 1 };
            let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : 1 };
            for i in @iterate(0, unroll) {
                body(gid_x, gid_y + i * bdim_y, mask, arr_acc, tmp_acc, is_row);
            }
        });

        // row component
        acc(acc_dev(), dim, block, || {
            let bid_x = acc_bidx();
            let bdim_y = acc_bdimy();
            let gdim_x = acc_gdimx();
            let gid_x = acc_gidx();
            let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
            let is_row = true;

            // 0 --- 1 --- 2
            if (bid_x < bx) {                               // left: 0
                let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : 0 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 0 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, tmp_acc, out_acc, is_row);
                }
                return()
            }
            if (bid_x >= gdim_x-bx) {                       // right: 2
                let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : 2 };
                let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 2 };
                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * bdim_y, mask, tmp_acc, out_acc, is_row);
                }
                return()
            }
            // no border handling: 1
            let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : 1 };
            let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : 1 };
            for i in @iterate(0, unroll) {
                body(gid_x, gid_y + i * bdim_y, mask, tmp_acc, out_acc, is_row);
            }
        });
    } else {
        let hu = out.height / unroll;
        let Bounds2DCol = [
            (0               , by*block(1)),
            (by*block(1)     , hu - by*block(1)),
            (hu - by*block(1), hu)
        ];

        for region in @iterate(0, 3) {
            let bounds = Bounds2DCol(region);
            let dim = (out.width, bounds(1) - bounds(0), 1);

            let arr_acc = Acc { img : arr, o_x : 0, o_y : 0, width : arr.width, height : arr.height, region : region };
            let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : region };

            acc(acc_dev(), dim, block, || {
                let gid_x = acc_gidx();
                let gid_y = bounds(0) * unroll +
                            acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
                let is_row = false;

                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), mask, arr_acc, tmp_acc, is_row);
                }
            });
        }

        let Bounds2DRow = [
            (0                      , bx*block(0)),
            (bx*block(0)            , out.width - bx*block(0)),
            (out.width - bx*block(0), out.width)
        ];

        for region in @iterate(0, 3) {
            let bounds = Bounds2DRow(region);
            let dim = (bounds(1) - bounds(0), out.height/unroll, 1);

            let tmp_acc = Acc { img : tmp, o_x : 0, o_y : 0, width : tmp.width, height : tmp.height, region : region };
            let out_acc = Acc { img : out, o_x : 0, o_y : 0, width : out.width, height : out.height, region : region };

            acc(acc_dev(), dim, block, || {
                let gid_x = bounds(0) + acc_gidx();
                let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;
                let is_row = true;

                for i in @iterate(0, unroll) {
                    body(gid_x, gid_y + i * acc_bdimy(), mask, tmp_acc, out_acc, is_row);
                }
            });
        }
    }
}

fn iteration_sep_advanced(mask : MaskSep, arr : Img, out : Img,
                          body : fn(int, int, MaskSep, Acc, Acc, bool) -> ()
                         ) -> () {
    let unroll = 1;
    let config = @compute_config((mask.size/2, mask.size/2), unroll);
    let dim = (out.width, out.height/unroll, 1);
    let block = config(0);

    // compute number of steps required to stage data to shared memory
    let m_range = mask.size / 2;
    let steps_x = 2;
    let offset_y = if (mask.size-1)%block(1)==0 { 0 } else { 1 };
    let steps_y = unroll + (mask.size-1)/block(1) + offset_y;

    // 0 -> generic
    // 1 -> global
    // 2 -> texture
    // 3 -> shared
    // 4 -> constant
    let dev = acc_dev();
    let gpu_arr = mmap(arr.data, dev, acc_tex(), 0, arr.width * out.height);
    let gpu_out = mmap(out.data, dev, 1,         0, out.width * out.height);

    acc(dev, dim, block, || {
        let tid_x = acc_tidx();
        let tid_y = acc_tidy();
        let bdim_x = acc_bdimx();
        let bdim_y = acc_bdimy();
        let gid_x = acc_gidx();
        let gid_y = acc_tidy() + acc_bdimy() * acc_bidy() * unroll;

        let lstride = block(0) + 2 * m_range;
        let lheight_col = block(1) * unroll + 2 * m_range;
        let lheight_row = block(1) * unroll;
        let mut smem_col = mmap(arr.data, dev, 3, 0, lstride * lheight_col);
        let     smem_row = mmap(arr.data, dev, 3, 0, lstride * lheight_row);
        let smr_img = Img { data : smem_row, width : lstride,  height : lheight_row, stride : lstride };
        let smc_img = Img { data : smem_col, width : lstride,  height : lheight_col, stride : lstride };
        let out_img = Img { data : gpu_out,  width : out.width, height : out.height, stride : out.width };
        let region = 1; // no bh


        for y in @range(0, steps_y) {
            let lid_y = tid_y + y*bdim_y;
            let mut idx_y = gid_y - m_range + y*bdim_y;
            idx_y = clamp_lower(idx_y, 0);
            idx_y = clamp_upper(idx_y, arr.height);
            for x in @range(0, steps_x) {
                let lid_x = tid_x + x*bdim_x;
                let mut idx_x = gid_x - m_range + x*bdim_x;
                idx_x = clamp_lower(idx_x, 0);
                idx_x = clamp_upper(idx_x, arr.width);

                if (lid_x < lstride && lid_y < lheight_col) {
                    smem_col(lid_y * lstride + lid_x) = gpu_arr(idx_y * arr.width + idx_x);
                }
            }
        }

        acc_barrier();

        for i in @iterate(0, unroll) {
            // index space: block
            let is_row = false;
            let out_acc = Acc { img : smr_img, o_x : m_range, o_y :           i * bdim_y, width : smr_img.width, height : smr_img.height, region : 1 /* no bh */ };
            let arr_acc = Acc { img : smc_img, o_x : m_range, o_y : m_range + i * bdim_y, width : smc_img.width, height : smc_img.height, region : 1 /* no bh */ };
            body(tid_x, tid_y, mask, arr_acc, out_acc, is_row);

            if (tid_x < m_range) {
                // left halo
                let out_acc = Acc { img : smr_img, o_x : 0, o_y :           i * bdim_y, width : smr_img.width, height : smr_img.height, region : 1 /* no bh */ };
                let arr_acc = Acc { img : smc_img, o_x : 0, o_y : m_range + i * bdim_y, width : smc_img.width, height : smc_img.height, region : 1 /* no bh */ };
                @body(tid_x, tid_y, mask, arr_acc, out_acc, is_row);
            }
            if (tid_x >= bdim_x-m_range) {
                // right halo
                let out_acc = Acc { img : smr_img, o_x : m_range + m_range, o_y :           i * bdim_y, width : smr_img.width, height : smr_img.height, region : 1 /* no bh */ };
                let arr_acc = Acc { img : smc_img, o_x : m_range + m_range, o_y : m_range + i * bdim_y, width : smc_img.width, height : smc_img.height, region : 1 /* no bh */ };
                @body(tid_x, tid_y, mask, arr_acc, out_acc, is_row);
            }
        }

        acc_barrier();

        for i in @iterate(0, unroll) {
            // index space: block
            let is_row = true;
            let out_acc = Acc { img : out_img, o_x : acc_bdimx() * acc_bidx(), o_y : acc_bdimy() * acc_bidy() * unroll + i * bdim_y, width : out_img.width, height : out_img.height, region : 1 /* no bh */ };
            let arr_acc = Acc { img : smr_img, o_x : m_range,                  o_y : i * bdim_y,                                     width : smr_img.width, height : smr_img.height, region : 1 /* no bh */ };
            body(tid_x, tid_y, mask, arr_acc, out_acc, is_row);
        }
    });
}

fn iteration_multi(width : int, height : int,
                   arr : &[float], out : &[float],
                   body : fn(int, int, &[float], &[float]) -> ()
                  ) -> () {
    let dim = (width, height/2, 1);
    let block = (128, 1, 1);

    let dev_gpu1 = 0;
    let map_arr_GPU1 = mmap(arr, dev_gpu1, 1, 0, width * height/2);
    let map_out_GPU1 = mmap(out, dev_gpu1, 1, 0, width * height/2);

    acc(dev_gpu1, dim, block, || {
        let gid_x = acc_gidx();
        let gid_y = acc_gidy();

        @body(gid_x, gid_y, map_arr_GPU1, map_out_GPU1);
    });

    let dev_gpu2 = 1;
    let map_arr_GPU2 = mmap(arr, dev_gpu2, 1, width * height/2, width * height/2);
    let map_out_GPU2 = mmap(out, dev_gpu2, 1, width * height/2, width * height/2);

    acc(dev_gpu2, dim, block, || {
        let gid_x = acc_gidx();
        let gid_y = acc_gidy();

        @body(gid_x, gid_y, map_arr_GPU2, map_out_GPU2);
    });
}
